buildscript {
  ext {
    ghPagesFolder = "$projectDir.parent/docs"
  }
}

plugins {
  id "base"
  id "org.asciidoctor.convert" version "1.5.6"
}

group "daggerok"
version "0.0.1"

asciidoctorj {
  version = "1.5.5"
}

asciidoctor {
  sourceDir = project.file("src")
  sources {
    include "index.adoc"
  }
  outputDir = project.file("$buildDir/asciidoc")
  attributes toc: "left",
      idprefix: "",
      docinfo1: "",
      icons: "font",
      idseparator: "-",
      setanchors: "true",
      "source-highlighter": "coderay",
      "toc-title": "Table of Contents",
      "build-gradle": file("build.gradle"),
      "endpoint-url": "https://daggerok.github.io/akka-examples"
}

task docs(type: Copy) {
  from "$buildDir/asciidoc/html5"
  from "$projectDir/src/static"
  into ghPagesFolder
  dependsOn asciidoctor
  shouldRunAfter clean, asciidoctor, build
}

clean.doFirst {
  delete ghPagesFolder
}

assemble.dependsOn asciidoctor
asciidoctor.shouldRunAfter clean

task wrapper(type: Wrapper) {
  gradleVersion = "4.4.1"
  distributionType = "ALL"
}
